buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

version '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

dependencies {
    compile 'org.jfree:jfreechart:1.0.19'
    compile 'com.github.pellierd:pddl4j:3.8.1'
    compile name: 'pgslookandfeel-1.1.2'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
}

jar {
    baseName = 'pddl4gui'
    version = '1.0'
    manifest {
        attributes 'Implementation-Title': 'PDDL4GUI build',
                'Implementation-Version': version,
                'Gradle-Version':'Gradle 4.8',
                'Built-By': 'Emmanuel Hermellin',
                'Main-Class': 'pddl4gui.Pddl4Gui'
    }
    doFirst {
        from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    }
}

task run (type: JavaExec, dependsOn: build){
    if (project.hasProperty("Args")) {
        println("arguments list : " + Args)
        args(Args.split(','))
    }
    jvmArgs = ["-javaagent:libs/pddl4j-3.8.jar", "-server", "-Xms2048m", "-Xmx2048m"]

    description = "Run PDDL4GUI"
    main = "pddl4gui.Pddl4Gui"
    classpath = sourceSets.main.runtimeClasspath
}

clean{
    delete 'build', 'out', 'pddl4gui-'+version+'.jar'
}

checkstyle {
    toolVersion = "8.9"
}

checkstyleMain.onlyIf() { project.hasProperty('checkstyle') }

checkstyleMain.doLast() {
    printCheckstyleVersion()
}

findbugs {
    effort= "max"
    reportLevel = "low"
    excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
}

tasks.withType(Jar) {
    destinationDir = file("$rootDir")
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
}

def printCheckstyleVersion(){
    println("Checkstyle version: "+checkstyle.toolVersion)
}